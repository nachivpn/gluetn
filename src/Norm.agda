--------------------------------
-- Normalization construction --
--------------------------------

module Norm where

open import Lib
open import CLT

private
  variable
    a b c : Ty

-- interpretation of types in the model
âŸ¦_âŸ§ : Ty â†’ Set
âŸ¦  Nat  âŸ§ = â„•
âŸ¦ a â‡’ b âŸ§ = Nf (a â‡’ b) Ã— (âŸ¦ a âŸ§ â†’ âŸ¦ b âŸ§)
âŸ¦ a + b âŸ§ = âŸ¦ a âŸ§ âŠ âŸ¦ b âŸ§
âŸ¦   ğŸ˜   âŸ§ = âŠ¥
âŸ¦   ğŸ™   âŸ§ = âŠ¤

-- reify values in the model to normal forms
reify : âŸ¦ a âŸ§ â†’ Nf a
reify {Nat}   zero     = Zero
reify {Nat}   (suc x)  = Succâˆ™ (reify x)
reify {a â‡’ b} (t , _)  = t
reify {a + b} (injâ‚ x) = Inlâˆ™ (reify x)
reify {a + b} (injâ‚‚ y) = Inrâˆ™ (reify y)
reify {ğŸ™}      tt      = Unit

-- "quote" values in the model into terms
-- p.s. cannot be named "quote" since it's an Agda keyword
quot : âŸ¦ a âŸ§ â†’ Tm a
quot v = em (reify v)

infixl 5 _âˆ™'_

-- semantic application
_âˆ™'_ : âŸ¦ a â‡’ b âŸ§ â†’ âŸ¦ a âŸ§ â†’ âŸ¦ b âŸ§
_âˆ™'_ (_ , f) x = f x

-- semantic recursion
rec' : âŸ¦ a âŸ§ â†’ âŸ¦ Nat â‡’ a â‡’ a âŸ§ â†’ âŸ¦ Nat âŸ§ â†’ âŸ¦ a âŸ§
rec' b f zero = b
rec' b f (suc n) = f âˆ™' n âˆ™' (rec' b f n)

-- semantic case
case' : âŸ¦ a â‡’ c âŸ§ â†’ âŸ¦ b â‡’ c âŸ§ â†’ âŸ¦ a + b âŸ§ â†’ âŸ¦ c âŸ§
case' f g (injâ‚ x) = f âˆ™' x
case' f g (injâ‚‚ y) = g âˆ™' y

-- interpretation of terms in the model
eval : Tm a â†’ âŸ¦ a âŸ§
eval K    = K , Î» x â†’ (Kâˆ™ (reify x)) , Î» _ â†’ x
eval S    = S , Î» g â†’
  Sâˆ™ (reify g) , Î» f â†’
    Sâˆ™âˆ™ (reify g) (reify f) , Î» x â†’
      (g âˆ™' x) âˆ™' (f âˆ™' x)
eval Zero = zero
eval Succ = Succ , suc
eval Rec  = Rec , Î» b â†’
  Recâˆ™ (reify b) , Î» f  â†’
    Recâˆ™âˆ™ (reify b) (reify f) , Î» n â†’
      rec' b f n
eval (t âˆ™ u) = (eval t) âˆ™' (eval u)
eval Inl = Inl , injâ‚
eval Inr = Inr , injâ‚‚
eval (Case)  = Case , Î» f â†’
  Caseâˆ™ (reify f) , Î» g â†’
    Caseâˆ™âˆ™ (reify f) (reify g) , Î» s â†’
      case' f g s
eval Init = Init , âŠ¥-elim
eval Unit = tt

-- normalization function
norm : Tm a â†’ Nf a
norm t = reify (eval t)
